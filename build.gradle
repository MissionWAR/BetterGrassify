plugins {
    id "dev.architectury.loom" version "1.7.+"
    id "me.modmuss50.mod-publish-plugin" version "0.6.3"
}

def loader = property("loom.platform")
if (property("loom.platform") == "neoforge")
    loader = "neo"
def isFabric = loader == "fabric"
def isNeo = loader == "neo"
def isForge = loader == "forge"

version = project.mod_version + "+" + loader + "." + property("deps.minecraft_version")
group = project.maven_group

base {
    archivesName = project.mod_name
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    maven { url "https://maven.su5ed.dev/releases" }
    maven { url "https://maven.neoforged.net/releases" }
}

loom {
    runConfigs.all {
        ideConfigGenerated true
        runDir "../../run"
    }

    if (loader == "forge") {
        forge {
            mixinConfig "bettergrass.mixins.json"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${property("deps.minecraft_version")}"
    mappings(loom.layered {
        mappings("net.fabricmc:yarn:${property("deps.yarn_mappings")}:v2")
        if (stonecutter.eval(property("deps.minecraft_version"), ">=1.21")) {
            mappings("dev.architectury:yarn-mappings-patch-neoforge:${property("deps.layered_mappings")}")
        }
    })

    if (isFabric) {
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        modImplementation "net.fabricmc.fabric-api:fabric-api:${property("deps.fapi_version")}"
        modImplementation "maven.modrinth:modmenu:${property("deps.modmenu_version")}"
        modImplementation "maven.modrinth:sodium:${property("deps.sodium_version")}"
    } else if (isNeo) {
        neoForge "net.neoforged:neoforge:${property("deps.neoforge")}"

        modImplementation "org.sinytra.forgified-fabric-api:forgified-fabric-api:${property("deps.fapi_version")}"
        modImplementation "maven.modrinth:sodium:${property("deps.sodium_version")}"
    } else if (isForge) {
        forge "net.minecraftforge:forge:${property("deps.fml")}"

        modImplementation "dev.su5ed.sinytra.fabric-api:fabric-api:${property("deps.fapi_version")}"
        compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.1"))
        modImplementation(include("io.github.llamalad7:mixinextras-forge:0.4.1"))
        modImplementation "maven.modrinth:embeddium:${property("deps.embeddium_version")}"
    }

    modImplementation "maven.modrinth:yacl:${property("deps.yacl_version")}"
}

if (stonecutter.current.isActive) {
    rootProject.tasks.register("buildActive") {
        group = "project"
        dependsOn(tasks.named("build"))
        dependsOn(tasks.named("publishMods"))
    }
}

afterEvaluate {
    stonecutter {
        stonecutter.const("fabric", isFabric)
        stonecutter.const("neo", isNeo)
        stonecutter.const("forge", isForge)
    }
}

processResources {
    var replaceProperties = [
            minecraft_range      : project.property("deps.mc_range"),
            mod_id               : mod_id,
            mod_name             : mod_name,
            mod_license          : mod_license,
            mod_version          : project.version,
            mod_authors          : mod_authors,
            mod_description      : mod_description,
            sodium_incompat_range: project.property("deps.sodium_incompat_range"),
            mixins               : project.property("deps.mixins")
    ]
    replaceProperties.each { key, value -> inputs.property(key, value) }

    filesMatching("bettergrass.mixins.json") {
        expand replaceProperties
    }

    if (isFabric) {
        filesMatching("fabric.mod.json") {
            expand replaceProperties
        }
        exclude(["META-INF/mods.toml", "META-INF/neoforge.mods.toml"])
    } else if (isNeo) {
        filesMatching("META-INF/neoforge.mods.toml") {
            expand replaceProperties
        }
        exclude(["fabric.mod.json", "META-INF/mods.toml"])
    } else if (isForge) {
        filesMatching("META-INF/mods.toml") {
            expand replaceProperties
        }
        exclude(["fabric.mod.json", "META-INF/neoforge.mods.toml"])
    }
}

def targetJavaVersion = 21
if ("${project.version}".contains("1.20.1")) {
    targetJavaVersion = 17
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = targetJavaVersion
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}"}
    }
}

publishMods {
    changelog = "# ${project.version}\n" + file("CHANGELOG.md").text
    type = STABLE
    file = remapJar.archiveFile
    displayName = "BetterGrassify ${project.version}"
    def deps

    if (isFabric){
        deps = ["fabric-api", "yacl"]
        modLoaders.addAll("fabric", "quilt")
    } else if (isNeo) {
        deps = ["forgified-fabric-api", "yacl"]
        modLoaders.add("neoforge")
    } else {
        deps = ["forgified-fabric-api", "yacl"]
        modLoaders.add("forge")
    }

    if ("${project.version}".contains("1.20.1")) {
        modrinth("m1.20.x") {
            projectId = "m5T5xmUy"
            accessToken = providers.environmentVariable("MODRINTH_TOKEN")
            requires(deps[0], deps[1])
            if (isFabric) {
                minecraftVersionRange {
                    start = "1.20"
                    end = "1.20.4"
                }
            } else {
                minecraftVersions.add("1.20.1")
            }

            // Discord
            announcementTitle = "Download from Modrinth"
        }

        curseforge("c1.20.x") {
            projectId = "1026394"
            accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")
            requires(deps[0], deps[1])
            if (isFabric) {
                minecraftVersionRange {
                    start = "1.20"
                    end = "1.20.4"
                }
            } else {
                minecraftVersions.add("1.20.1")
            }

            // Discord
            announcementTitle = "Download from CurseForge"
            projectSlug = "bettergrassify"
        }
    }

    if ("${project.version}".contains("1.20.6")) {
        modrinth("m1.20.6") {
            projectId = "m5T5xmUy"
            accessToken = providers.environmentVariable("MODRINTH_TOKEN")
            requires(deps[0], deps[1])
            minecraftVersionRange {
                start = "1.20.5"
                end = "1.20.6"
            }

            // Discord
            announcementTitle = "Download from Modrinth"
        }

        curseforge("c1.20.6") {
            projectId = "1026394"
            accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")
            requires(deps[0], deps[1])
            minecraftVersionRange {
                start = "1.20.5"
                end = "1.20.6"
            }

            // Discord
            announcementTitle = "Download from CurseForge"
            projectSlug = "bettergrassify"
        }
    }

    if ("${project.version}".contains("1.21")) {
        modrinth("m1.21") {
            projectId = "m5T5xmUy"
            accessToken = providers.environmentVariable("MODRINTH_TOKEN")
            requires(deps[0], deps[1])
            minecraftVersionRange {
                start = "1.21"
                end = "1.21.1"
            }

            // Discord
            announcementTitle = "Download from Modrinth"
        }

        curseforge("c1.21") {
            projectId = "1026394"
            accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")
            requires(deps[0], deps[1])
            minecraftVersionRange {
                start = "1.21"
                end = "1.21.1"
            }

            // Discord
            announcementTitle = "Download from CurseForge"
            projectSlug = "bettergrassify"
        }
    }

    github {
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        repository = "UltimatChamp/BetterGrassify"
        commitish = "main"

        // Discord
        announcementTitle = "Download from GitHub"
    }

    discord {
        webhookUrl = providers.environmentVariable("DISCORD_WEBHOOK")
        username = "BetterGrassify Releases"
        avatarUrl = "https://cdn.modrinth.com/data/m5T5xmUy/c67c1f900e8344e462bb5c21fb512579f3b0be46.png"
    }
}
